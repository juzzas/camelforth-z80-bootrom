HEX
 4000 CONSTANT 1FIXED
 4666 CONSTANT 1.1FIXED
10000 CONSTANT 4FIXED
DECIMAL
1FIXED  3 * 80 / CONSTANT XINC
1.1FIXED 2* 24 / CONSTANT YINC
: *F ( F G -- F*G ) 1FIXED */ ;
: SQ ( F -- F F*F ) OVER DUP *F ;
: MANDEL
  1.1FIXED DUP NEGATE DO
    1FIXED DUP 2* NEGATE DO
      I J 30                 ( INITIAL POINT X,Y AND MAX ITERATION COUNT )
      BEGIN  1- ?DUP
      WHILE  ROT ROT SQ SQ
             2DUP + 4FIXED <
      WHILE  - I +
             ROT ROT *F 2* J + ROT
      REPEAT 2DROP DROP          \ EXIT FROM SECOND WHILE
             SPACE
      ELSE   ." *"               \ EXIT FROM FIRST WHILE
      THEN 2DROP
    XINC +LOOP
    CR
  YINC +LOOP ;
MANDEL

